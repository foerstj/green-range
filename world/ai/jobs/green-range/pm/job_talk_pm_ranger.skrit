Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		m_Go$.GetConversation.RSSelectConversation( "pm_ranger" );

		if ( GameAuditor.GetDb.GetBool( "party_accept_0x1f00003" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "pm_accept" );
			GameAuditor.GetDb.SetBool( "party_accept_0x1f00003", false );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_decline_0x1f00003" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "pm_reject" );
			GameAuditor.GetDb.SetBool( "party_decline_0x1f00003", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x1f00003", true );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_disband_0x1f00003" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "pm_disband" );
			GameAuditor.GetDb.SetBool( "party_disband_0x1f00003", false );
			GameAuditor.GetDb.SetBool( "party_disbanded_0x1f00003", true );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x1f00003" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "pm_rejoin" );			
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x1f00003" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "pm_rejoin" );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x1f00003", false );
			TryAnim$('talk');
		}  else {
			TryAnim$('ulo1');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}

state Exiting$
{
	event OnEnterState$
	{
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


